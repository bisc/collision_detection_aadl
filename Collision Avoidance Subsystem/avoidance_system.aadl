package avoidance_system
	public
	with prediction_system;
	with vehicle;
	with basic_devices;
	with basic_debian;
	
	thread event_handler
		features
			read_avoidable: in event data port;
			read_unavoidable: in event data port;
			send_cmd: out event data port;
			send_driver_alert: out event data port;
	end event_handler;
	
	thread implementation event_handler.singleton
	end event_handler.singleton;
	
	thread data_handler
		features
			read_vehicle_sensors: in data port;
			send_proc_data: out data port;
	end data_handler;
	
	thread implementation data_handler.singleton
	end data_handler.singleton;
	
	thread control_laws
		features
			predictor_cmd: in event port;
			read_proc_data: in data port;
			send_steering_cmds: out data port;
			send_brake_cmds:  out event data port;
	end control_laws;
	
	thread control_laws_avoidable extends control_laws
	end control_laws_avoidable;
	
	thread implementation control_laws_avoidable.singleton
	end control_laws_avoidable.singleton;
	
	thread control_laws_unavoidable extends control_laws
	end control_laws_unavoidable;
	
	thread implementation control_laws_unavoidable.singleton
	end control_laws_unavoidable.singleton;
	
	process collision_threat_handler
		features
			predictor_input: in event data port;
			vehicle_event_output: out event data port;
			sensor_data: in data port;
			vehicle_control_data: out data port;
	end collision_threat_handler;
	
	process implementation collision_threat_handler.singleton
		subcomponents
			collision_event_handler: thread event_handler.singleton;
			collidable_objects_data: thread data_handler.singleton;
			avoid_collision_control: thread control_laws_avoidable.singleton;
			safe_impact_control: thread control_laws_unavoidable.singleton;
		connections
			EC1: port predictor_input -> collision_event_handler.read_avoidable;
			EC2: port predictor_input -> collision_event_handler.read_unavoidable;
			EC3: port collision_event_handler.send_cmd -> avoid_collision_control.predictor_cmd;
			EC4: port collision_event_handler.send_cmd -> safe_impact_control.predictor_cmd;
			EC5: port collision_event_handler.send_driver_alert -> vehicle_event_output;
			DC1: port sensor_data -> collidable_objects_data.read_vehicle_sensors;
			DC2: port collidable_objects_data.send_proc_data -> avoid_collision_control.read_proc_data;
			DC3: port collidable_objects_data.send_proc_data -> safe_impact_control.read_proc_data;
			DC4: port avoid_collision_control.send_brake_cmds -> vehicle_control_data;
			DC5: port safe_impact_control.send_brake_cmds -> vehicle_control_data;
			DC6: port avoid_collision_control.send_steering_cmds -> vehicle_control_data;
			DC7: port safe_impact_control.send_steering_cmds -> vehicle_control_data;
	end collision_threat_handler.singleton;
	
	system avoidance_system
	end avoidance_system;
	
	system implementation avoidance_system.impl
		subcomponents
			avoidance_unit: process collision_threat_handler.singleton;
			vehicle_processor: processor basic_debian::real_time.one_ghz;
			vehicle_memory: memory basic_debian::ram.standard;
			vehicle_bus: bus basic_debian::basic_bus.standard;
			bus_driver: device basic_devices::bus_driver.standard;
			event_distributor: device basic_devices::event_distributor.standard;
		connections
			get_predictor_msg: port event_distributor.event_data -> avoidance_unit.predictor_input;
			get_vehicle_feedback: port event_distributor.event_data -> avoidance_unit.sensor_data;
			send_alert_to_operator: port avoidance_unit.vehicle_event_output -> bus_driver.event_data;
			vehicle_control: port avoidance_unit.vehicle_control_data-> bus_driver.event_data;
			event_driver: bus access bus_driver.ba <-> vehicle_bus;
				
		properties
			Allowed_Processor_Binding=>(reference (vehicle_processor)) applies to avoidance_unit;
			Allowed_Memory_Binding=>(reference (vehicle_memory)) applies to avoidance_unit;
	end avoidance_system.impl;
	
end avoidance_system;
