-- Copyright (c) 2015 Carnegie Mellon University and Ivan Ruchkin.
-- All Rights Reserved.
--
-- Released under a modified MIT license, please see license.txt or contact permission@sei.cmu.edu for full terms.  DM-0002665
--
-- This Software includes and/or makes use of the following Third-Party Software subject to its own license:
-- collision_detection_aadl
-- ========================
--
-- A AADL model of a collision prevention system for use in a telematics architecture
--
-- License
-- =======
--
-- The MIT License (MIT) 
-- Copyright (c) 2013 Nicholas Guzzardo, Mike Kabbani, Ethan McGee
-- 
-- Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
--
-- The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
--
-- THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


-- The user interaction subsystem handles the two-way interaction with the driver. 
-- It handles infotainment functionality and is almost completely separated from safety-critical systems.

package user_interaction_subsystem
	public
	with basic_computing;
	with basic_devices;
	with basic_sensors;
	
	-- plays audio through speakers
	thread play_audio
		features
			-- the text to be spoken
			text: in data port;
			-- the audio to be played on the speaker system
			audio: out data port;
		properties
			Dispatch_Protocol => Sporadic;	
			Compute_Execution_Time => 1ms .. 1000ms;
	end play_audio;
	
	thread implementation play_audio.standard
	end play_audio.standard;
	
	-- listening to the speaker
	thread listen_for_audio
		features
			-- audio is available for processing
			audio_available: out event data port;
		properties
			Dispatch_Protocol => Periodic;
			-- this needs to be fairly rapid
			Compute_Execution_Time => 1ms .. 10ms;
			Period => 50ms;
	end listen_for_audio;
	
	thread implementation listen_for_audio.standard
	end listen_for_audio.standard;
	
	-- speech to text
	thread convert_to_text
		features
			-- the audio spoken by the driver
			audio: in data port;
			-- the textual version of the audio
			text: out data port;
		properties
			Dispatch_Protocol => Sporadic;	
			Compute_Execution_Time => 1ms .. 1000ms;	
	end convert_to_text;
	
	thread implementation convert_to_text.standard
	end convert_to_text.standard;
	
	------------------------------------------------------------------------------------------
	
	process speaker_controller
		features
			message: in data port;
			audio: out data port;
	end speaker_controller;
	
	process implementation speaker_controller.standard
		subcomponents
			audio_player: thread play_audio.standard;
		connections
			text_to_convert: port message -> audio_player.text											{ Communication_Properties::Latency => 8ms .. 12ms; };
			audio_to_play: port audio_player.audio -> audio												{ Communication_Properties::Latency => 8ms .. 12ms; };
	end speaker_controller.standard;
	
	process microphone_controller
		features
			sensor_ready: in event data port;
			text: out data port;
	end microphone_controller;
	
	process implementation microphone_controller.standard
		subcomponents
			microphone_sensor: thread listen_for_audio.standard;
			microphone: thread convert_to_text.standard;
		connections
			audio_available: port microphone_sensor.audio_available -> microphone.audio					{ Communication_Properties::Latency => 8ms .. 12ms; };
			converted_audio: port microphone.text -> text												{ Communication_Properties::Latency => 8ms .. 12ms; };
	end microphone_controller.standard;
	
	------------------------------------------------------------------------------------------
	
	-- The full subsystem for user interaction
	system user_interaction_subsystem
		features
			message_received_from_user: out event data port;
			message_to_send_to_user: in event data port;
	end user_interaction_subsystem;
	
	system implementation user_interaction_subsystem.standard
		subcomponents
			microphone: device basic_devices::microphone.in_vehicle;
			speaker: device basic_devices::speaker.in_vehicle;
			system_spoken_to: device basic_sensors::sensor.system_spoken_to;
		
			speaker_controller: process speaker_controller.standard;
			microphone_controller: process microphone_controller.standard;
		
			vehicle_processor: processor basic_computing::real_time.one_ghz;
			vehicle_memory: memory basic_computing::ram.standard;
			vehicle_bus: bus basic_computing::basic_bus.standard;
		connections 
			microphone_connection: bus access vehicle_bus <-> microphone.ba								{ Communication_Properties::Latency => 8ms .. 12ms; };
			speaker_connection: bus access vehicle_bus <-> speaker.ba									{ Communication_Properties::Latency => 8ms .. 12ms; };
			
			microphone_ready: port microphone.spoken_command -> microphone_controller.sensor_ready		{ Communication_Properties::Latency => 8ms .. 12ms; };
			audio_to_play: port speaker_controller.audio -> speaker.audio								{ Communication_Properties::Latency => 8ms .. 12ms; };
			
			message_received: port microphone_controller.text -> message_received_from_user				{ Communication_Properties::Latency => 8ms .. 12ms; };
			message_to_play: port message_to_send_to_user -> speaker_controller.message					{ Communication_Properties::Latency => 8ms .. 12ms; };
		properties
			Allowed_Processor_Binding=>(reference (vehicle_processor)) applies to speaker_controller.audio_player;
			Allowed_Processor_Binding=>(reference (vehicle_processor)) applies to microphone_controller.microphone_sensor;
			Allowed_Processor_Binding=>(reference (vehicle_processor)) applies to microphone_controller.microphone;
		
			Allowed_Memory_Binding=>(reference (vehicle_memory)) applies to speaker_controller;
			Allowed_Memory_Binding=>(reference (vehicle_memory)) applies to microphone_controller;
	end user_interaction_subsystem.standard;
	
end user_interaction_subsystem;